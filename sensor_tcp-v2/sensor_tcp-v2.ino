
// Flash Size: "1MB (FS:256KB OTA:374KB)"

#include <ESP8266WiFi.h>
#include <ESPAsyncTCP.h>
#include <ESPAsyncWebServer.h>
#include <FS.h>
#include <LittleFS.h>
#include <ArduinoJson.h>
#include <ESP8266HTTPClient.h>
#include <WiFiClient.h> // For HTTPS, use WiFiClientSecure
#include <TaskScheduler.h>
// ===================================
// Pin Definitions for ESP-01 Board
// ===================================
// #define SERVER_PIN 3  //GPIO3 RX
// #define SENSOR_PIN 1  //GPIO1 TXD
// #define LED_PIN 2     // GPIO2

// ===================================
// Pin Definitions for ESP8266-12E / NodeMCU
// ===================================
#define SERVER_PIN   13  // GPIO13 (D7 on NodeMCU).
#define SENSOR_PIN   5  // GPIO5 (D1 on NodeMCU).
#define LED_PIN      2   // GPIO2 (D4 on NodeMCU - Often used for internal LED).

bool SENSOR_STAT = HIGH;
bool sensorAlarm = false;
bool isServerMode = false;

unsigned long lastSensorCheck = 0;
unsigned long lastServerAPI = 0;
unsigned long lastDiscordAPI = 0;
unsigned long lastDiscordSend = 0;
unsigned long lastApiSend = 0;

void serverMode();
void connectAP();
String chipID();
void save_Config(AsyncWebServerRequest *request);
bool loadConfig();
bool sendMessageToDiscord(String msg);
bool sendDataToAPI(String payload);
String report();

struct Api {
  bool trigger = false;
  int maxRequest = 5;
  bool test = false;  
}api;

struct Discord {
  bool trigger = false;
  int maxRequest = 5;
  bool test = false;  
}discord;
// ‡∏õ‡∏£‡∏∞‡∏Å‡∏≤‡∏® server ‡πÅ‡∏ö‡∏ö global
AsyncWebServer server(80);

struct Config {
  String id;
  String ssid;
  String password;
  String url; 
  String discord; 
  String esp_now_mac;
} cfg;

void setup() {
  Serial.begin(115200);
  // put your setup code here, to run once:
  pinMode(SERVER_PIN, INPUT_PULLUP);
  pinMode(SENSOR_PIN, INPUT_PULLUP);
  pinMode(LED_BUILTIN, OUTPUT);
  
  if(!LittleFS.begin()){    
    return;
  }

  int pinval = digitalRead(SERVER_PIN);
  if(!pinval){
    serverMode();
  }else if(loadConfig()){   
    connectAP(); 
  }
  
}

void serverMode(){
  isServerMode = true;
  // ‡πÄ‡∏£‡∏¥‡πà‡∏° ‡πÇ‡∏´‡∏°‡∏î Access Point ‡πÅ‡∏•‡∏∞ Webserver
  String apName = "esp_" + chipID();
  WiFi.softAP(apName.c_str(), "");
  delay(100); //‡∏´‡∏ô‡πà‡∏ß‡∏á‡πÄ‡∏ß‡∏•‡∏≤ 100 ms ‡πÄ‡∏û‡∏∑‡πà‡∏≠‡πÉ‡∏´‡πâ AP ‡πÄ‡∏£‡∏¥‡πà‡∏°‡∏ó‡∏≥‡∏á‡∏≤‡∏ô

  server.on("/", HTTP_GET,[](AsyncWebServerRequest *request){
    if(LittleFS.exists("/index.html")){
      request->send(LittleFS, "/index.html", "text/html");
    } else {
      int duration = millis();
      request->send(200, "text/html", "<center><br>‚ö†Ô∏èerror‚ö†Ô∏è<h1>ESP8266 Config</h1><p>index.html not found in LittleFS "+(String)duration+"</p></center>");
    }
  });

  server.on("/save_config", HTTP_GET,[](AsyncWebServerRequest *request){
    save_Config(request);
  });

  // ‡∏à‡∏±‡∏î‡∏Å‡∏≤‡∏£ request ‡∏ó‡∏µ‡πà‡πÑ‡∏°‡πà‡∏ï‡∏£‡∏á‡∏Å‡∏±‡∏ö route ‡πÉ‡∏î‡πÜ
  server.onNotFound([](AsyncWebServerRequest *request){    
    request->send(404, "text/plain", "Not Found "+request->url());
  });
  // ‡πÄ‡∏£‡∏¥‡πà‡∏° server
  server.begin();

}

bool loadConfig() {
  
  // 1. ‡πÄ‡∏õ‡∏¥‡∏î‡πÑ‡∏ü‡∏•‡πå‡∏™‡∏≥‡∏´‡∏£‡∏±‡∏ö‡∏≠‡πà‡∏≤‡∏ô
  File file = LittleFS.open("/config.json", "r");
  if (!file) {
    // ‡∏Ñ‡∏∑‡∏ô‡∏Ñ‡πà‡∏≤‡πÄ‡∏õ‡πá‡∏ô false ‡πÄ‡∏û‡∏∑‡πà‡∏≠‡∏ö‡∏≠‡∏Å‡∏ß‡πà‡∏≤‡πÇ‡∏´‡∏•‡∏î‡πÑ‡∏°‡πà‡∏™‡∏≥‡πÄ‡∏£‡πá‡∏à
    return false;
  }
  
  // 2. ‡πÅ‡∏¢‡∏Å‡∏ß‡∏¥‡πÄ‡∏Ñ‡∏£‡∏≤‡∏∞‡∏´‡πå JSON
  StaticJsonDocument<256> doc;
  
  // Deserialize the JSON document
  DeserializationError error = deserializeJson(doc, file);
  
  // ‡∏õ‡∏¥‡∏î‡πÑ‡∏ü‡∏•‡πå‡∏ó‡∏±‡∏ô‡∏ó‡∏µ‡∏´‡∏•‡∏±‡∏á‡∏à‡∏≤‡∏Å‡∏≠‡πà‡∏≤‡∏ô
  file.close(); 
  
  // 3. ‡∏ï‡∏£‡∏ß‡∏à‡∏™‡∏≠‡∏ö‡∏Ç‡πâ‡∏≠‡∏ú‡∏¥‡∏î‡∏û‡∏•‡∏≤‡∏î‡πÉ‡∏ô‡∏Å‡∏≤‡∏£‡πÅ‡∏¢‡∏Å‡∏ß‡∏¥‡πÄ‡∏Ñ‡∏£‡∏≤‡∏∞‡∏´‡πå
  if (error) {  
    // ‡∏Ñ‡∏∑‡∏ô‡∏Ñ‡πà‡∏≤‡πÄ‡∏õ‡πá‡∏ô false ‡πÄ‡∏û‡∏∑‡πà‡∏≠‡∏ö‡∏≠‡∏Å‡∏ß‡πà‡∏≤‡πÇ‡∏´‡∏•‡∏î‡πÑ‡∏°‡πà‡∏™‡∏≥‡πÄ‡∏£‡πá‡∏à  
    return false;
  }

  // ‡∏î‡∏∂‡∏á‡∏Ñ‡πà‡∏≤‡∏à‡∏≤‡∏Å JSON ‡∏•‡∏á‡πÉ‡∏ô‡∏™‡∏°‡∏≤‡∏ä‡∏¥‡∏Å‡∏Ç‡∏≠‡∏á Struct (Object) ‡∏ó‡∏µ‡πà‡∏ñ‡∏π‡∏Å‡∏™‡πà‡∏á‡πÄ‡∏Ç‡πâ‡∏≤‡∏°‡∏≤ (cfg)
  
  cfg.id          = doc["id"].as<String>();
  cfg.ssid        = doc["ssid"].as<String>();
  cfg.password    = doc["password"].as<String>();
  cfg.url         = doc["url"].as<String>();  
  cfg.discord     = doc["discord"].as<String>();  
  cfg.esp_now_mac = doc["esp_now_mac"].as<String>();

  return true;
}


void connectAP(){
  if(cfg.ssid != ""){
    WiFi.mode(WIFI_STA);
    WiFi.begin(cfg.ssid.c_str(), cfg.password.c_str());
    WiFi.setAutoReconnect(true);
    while (WiFi.status() != WL_CONNECTED) {
      digitalWrite(LED_BUILTIN, HIGH);
      delay(200);
      digitalWrite(LED_BUILTIN, LOW);
      delay(200);
    }

    if(WiFi.status() == WL_CONNECTED){
      discord.test = true;
      Serial.println(WiFi.localIP());
    }

  server.on("/", HTTP_GET,[](AsyncWebServerRequest *request){
  String html = "<!DOCTYPE html><html><head>";
  html += "<meta name='viewport' content='width=device-width, initial-scale=1'>";
  html += "<meta charset='UTF-8'>";
  html += "<style>";
  html += "body{font-family:Arial;margin:20px;background:#f0f0f0}";
  html += ".container{max-width:600px;margin:0 auto;background:white;padding:20px;border-radius:10px;box-shadow:0 2px 10px rgba(0,0,0,0.1)}";
  html += "h1{color:#333;text-align:center;margin-bottom:30px}";
  html += ".status-item{padding:10px;margin:10px 0;background:#f9f9f9;border-left:4px solid #4CAF50;border-radius:4px}";
  html += ".label{font-weight:bold;color:#555}";
  html += ".value{color:#333;float:right}";
  html += ".btn{display:block;width:100%;padding:15px;margin:20px 0;font-size:16px;font-weight:bold;";
  html += "color:white;background:#4CAF50;border:none;border-radius:5px;cursor:pointer;transition:0.3s}";
  html += ".btn:hover{background:#45a049}";
  html += ".btn:active{transform:scale(0.98)}";
  html += "#result{margin-top:20px;padding:15px;border-radius:5px;display:none;text-align:center;font-weight:bold}";
  html += ".success{background:#d4edda;color:#155724;border:1px solid #c3e6cb}";
  html += ".error{background:#f8d7da;color:#721c24;border:1px solid #f5c6cb}";
  html += "</style></head><body>";
  
  html += "<div class='container'>";
  html += "<h1>üîß ESP8266 Status</h1>";
  
  // WiFi Status
  html += "<div class='status-item'>";
  html += "<span class='label'>WiFi:</span>";
  html += "<span class='value'>" + String(WiFi.isConnected() ? "‚úÖ Connected" : "‚ùå Disconnected") + "</span>";
  html += "</div>";
  
  // IP Address
  html += "<div class='status-item'>";
  html += "<span class='label'>IP Address:</span>";
  html += "<span class='value'>" + WiFi.localIP().toString() + "</span>";
  html += "</div>";
  
  // Signal Strength
  html += "<div class='status-item'>";
  html += "<span class='label'>Signal (RSSI):</span>";
  html += "<span class='value'>" + String(WiFi.RSSI()) + " dBm</span>";
  html += "</div>";
  
  // Free Memory
  html += "<div class='status-item'>";
  html += "<span class='label'>Free Heap:</span>";
  html += "<span class='value'>" + String(ESP.getFreeHeap()) + " bytes</span>";
  html += "</div>";
  
  // Uptime
  html += "<div class='status-item'>";
  html += "<span class='label'>Uptime:</span>";
  html += "<span class='value'>" + String(millis()/1000) + " seconds</span>";
  html += "</div>";
  
  // Test Button
  html += "<button class='btn' onclick='sendTest()'>üì§ Send Test Message to Discord</button>";
  
  // Result message area
  html += "<div id='result'></div>";
  
  html += "</div>";
  
  // JavaScript
  html += "<script>";
  html += "function sendTest(){";
  html += "  document.getElementById('result').style.display='none';";
  html += "  fetch('/test')";
  html += "    .then(response => response.text())";
  html += "    .then(data => {";
  html += "      var resultDiv = document.getElementById('result');";
  html += "      resultDiv.className = 'success';";
  html += "      resultDiv.textContent = '‚úÖ ' + data;";
  html += "      resultDiv.style.display = 'block';";
  html += "      setTimeout(() => location.reload(), 2000);";
  html += "    })";
  html += "    .catch(error => {";
  html += "      var resultDiv = document.getElementById('result');";
  html += "      resultDiv.className = 'error';";
  html += "      resultDiv.textContent = '‚ùå Error: ' + error;";
  html += "      resultDiv.style.display = 'block';";
  html += "    });";
  html += "}";
  html += "</script>";
  
  html += "</body></html>";
  
  request->send(200, "text/html", html);
});

    server.on("/test", HTTP_GET,[](AsyncWebServerRequest *request){
      discord.test = true;
      request->send(200, "text/plain", "Message queued");    
    });

     // ‡∏à‡∏±‡∏î‡∏Å‡∏≤‡∏£ request ‡∏ó‡∏µ‡πà‡πÑ‡∏°‡πà‡∏ï‡∏£‡∏á‡∏Å‡∏±‡∏ö route ‡πÉ‡∏î‡πÜ
  server.onNotFound([](AsyncWebServerRequest *request){    
    request->send(404, "text/plain", "Not Found "+request->url());
  });
  // ‡πÄ‡∏£‡∏¥‡πà‡∏° server
  server.begin();
  
  }
}

String report(){
      
      String statusMsg = "üîß **ESP8266 Status Report**\\n";
      statusMsg += "**ID:** " + cfg.id +"\\n";
      statusMsg += "**Sensor:** " + String(digitalRead(SENSOR_PIN) ? "üü¢ ‡∏õ‡∏Å‡∏ï‡∏¥":"üî¥ ‡∏â‡∏∏‡∏Å‡πÄ‡∏â‡∏¥‡∏ô")+"\\n";
      statusMsg += "**WiFi:** " + String(WiFi.isConnected() ? "Connected" : "Disconnected") + "\\n";
      statusMsg += "**IP Address:** " + WiFi.localIP().toString() + "\\n";
      statusMsg += "**Signal (RSSI):** " + String(WiFi.RSSI()) + " dBm\\n";
      statusMsg += "**Free Heap:** " + String(ESP.getFreeHeap()) + " bytes\\n";
      statusMsg += "**Uptime:** " + String(millis()/1000) + " seconds\\n";
      return statusMsg;
}

String chipID(){
    uint32_t chipId = ESP.getChipId();
    String chipIDStr = String(chipId, HEX);
    chipIDStr.toUpperCase();
    return chipIDStr;
}

void save_Config(AsyncWebServerRequest *request){
  if (!request->hasParam("id")) return request->send(400, "application/json", "{\"error\":\"No parameters ID\"}");
  if (!request->hasParam("ssid")) return request->send(400, "application/json", "{\"error\":\"No parameters SSID\"}");
  if (!request->hasParam("password")) return request->send(400, "application/json", "{\"error\":\"No parameters PASSWORD\"}");
  if (!request->hasParam("url")) return request->send(400, "application/json", "{\"error\":\"No parameters URL\"}");
  
  
  // ‡∏£‡∏±‡∏ö‡∏Ñ‡πà‡∏≤‡∏à‡∏≤‡∏Å request ‡∏°‡∏≤‡πÄ‡∏Å‡πá‡∏ö‡∏•‡∏á‡∏ï‡∏±‡∏ß‡πÅ‡∏õ‡∏£
  String id         = request->getParam("id")->value();
  String ssid         = request->getParam("ssid")->value();
  String password     = request->getParam("password")->value();
  String url          = request->getParam("url")->value();
  String discord      = "";
  String esp_now_mac  = "";
  
  if (request->hasParam("discord")){
    discord = request->getParam("discord")->value();
  }
  if (request->hasParam("esp_now_mac")){
    esp_now_mac  = request->getParam("esp_now_mac")->value();
  }
  

  File file = LittleFS.open("/config.json","w");
  if(!file){
    return request->send(400, "application/json", "{\"error\":\"Failed to create new config file\"}");
  }

  // ‡πÉ‡∏ä‡πâ ArduinoJson
  StaticJsonDocument<256> doc;
  doc["id"]           = id;
  doc["ssid"]         = ssid;
  doc["password"]     = password;
  doc["url"]          = url;  
  doc["discord"]      = discord;
  doc["esp_now_mac"]  = esp_now_mac;
  // ‡πÄ‡∏Ç‡∏µ‡∏¢‡∏ô JSON ‡∏•‡∏á‡πÑ‡∏ü‡∏•‡πå
  if(serializeJson(doc, file) == 0){
    file.close();
    return request->send(500, "application/json", "{\"error\":\"Failed to write to file\"}");
  }
  file.close();
  request->send(200, "application/json", "{\"success\":\"Configuration saved successfully\"}");

}

bool sendMessageToDiscord(String msg = ""){
  if(WiFi.status() != WL_CONNECTED){
    return false;
  }  
  if(msg == "") msg = report();
    
    WiFiClientSecure *client = new WiFiClientSecure();
    client->setInsecure(); // ‡πÑ‡∏°‡πà‡∏ï‡∏£‡∏ß‡∏à‡∏™‡∏≠‡∏ö SSL certificate 
    HTTPClient http;

    if(http.begin(*client, cfg.discord)){
      http.addHeader("Content-Type", "application/json");
      http.addHeader("User-Agent", "ESP-Discord-Bot");
      String jsonPayload = "{\"content\": \"" + msg + "\"}";
      // ‡∏™‡πà‡∏á POST request
      int httpResponseCode = http.POST(jsonPayload);    
      if(httpResponseCode > 0){
        Serial.print("Discord http Code: ");
        Serial.println(httpResponseCode);
        http.end();
        delete client;  // ‡∏¢‡πâ‡∏≤‡∏¢‡∏°‡∏≤‡∏´‡∏•‡∏±‡∏á http.end()
        return true;
      }else{
        Serial.print("Error Discord http Code: ");
        Serial.println(httpResponseCode);
        http.end();
        delete client;  // ‡∏¢‡πâ‡∏≤‡∏¢‡∏°‡∏≤‡∏´‡∏•‡∏±‡∏á http.end()
        return false;
      }
    }else{
      return false;
    }
  return false;
}

bool sendDataToAPI(String payload) {  

  Serial.println(payload);

  if (WiFi.status() != WL_CONNECTED) {
    Serial.println("Wifi Not connected");
    return false;
  }

  HTTPClient http;
  WiFiClient *client = nullptr;
  WiFiClientSecure *secureClient = nullptr;
  
  if (cfg.url.startsWith("https")) {
    secureClient = new WiFiClientSecure();
    secureClient->setInsecure();
    http.begin(*secureClient, cfg.url);
  } else if(cfg.url.startsWith("http")) {
    client = new WiFiClient();
    http.begin(*client, cfg.url);
  }else{
    http.end();
    // ‡∏•‡∏ö client ‡∏´‡∏•‡∏±‡∏á‡∏à‡∏≤‡∏Å http.end()
    if (secureClient) delete secureClient;
    if (client) delete client;
    sendMessageToDiscord("‚ö†Ô∏è Error URL ‡πÑ‡∏°‡πà‡∏ñ‡∏π‡∏Å‡∏ï‡πâ‡∏≠‡∏á ‚ùåÔ∏è "+cfg.url);
    return false;
  }
  
  http.addHeader("Content-Type", "application/json");
  //http.setTimeout(5000); // ‡πÄ‡∏û‡∏¥‡πà‡∏° timeout 5 ‡∏ß‡∏¥‡∏ô‡∏≤‡∏ó‡∏µ
  int httpResponseCode = http.POST(payload);
  if (httpResponseCode > 0){
    // ‡∏™‡∏≤‡∏°‡∏≤‡∏£‡∏ñ‡∏™‡πà‡∏á‡πÑ‡∏õ‡∏¢‡∏±‡∏á‡πÄ‡∏ã‡∏¥‡∏£‡πå‡∏û‡πÄ‡∏ß‡∏≠‡∏£‡πå‡πÑ‡∏î‡πâ‡∏ô‡∏∞
    Serial.print("HTTP Response code: ");
    Serial.println(httpResponseCode);
    http.end();  
    if (secureClient) delete secureClient;
    if (client) delete client;
    return true;  
  }else{
    // Error on sending POST. Code:
    Serial.print("Error on sending POST. Code ");
    Serial.println(httpResponseCode);
    http.end();  
    if (secureClient) delete secureClient;
    if (client) delete client;
    return false;
  }
  return false;
}


void loop() {
  unsigned long now = millis();  
//-----------------------------------------------------------------------
  if(isServerMode){
    delay(10); // ‡πÉ‡∏´‡πâ ESP8266 ‡∏ó‡∏≥‡∏á‡∏≤‡∏ô‡∏û‡∏∑‡πâ‡∏ô‡∏´‡∏•‡∏±‡∏á
    return;
  }
//-----------------------------------------------------------------------
  // Task 1: Check Sensor ‡∏ó‡∏∏‡∏Å 100 ms
  if (now - lastSensorCheck >= 100) {
    SENSOR_STAT     = digitalRead(SENSOR_PIN);
    lastSensorCheck = now;
  }
//-----------------------------------------------------------------------
  // Task 2: Server API ‡∏ó‡∏∏‡∏Å 500 ms
  if (now - lastServerAPI >= 500) {
//    Serial.println("Task 2: Server API ‡∏ó‡∏∏‡∏Å 500 ms");
    discord.trigger = !SENSOR_STAT;
    api.trigger     = !SENSOR_STAT;
    lastServerAPI   = now;
  }

  //-----------------------------------------------------------------------
//  ‡∏™‡πà‡∏á‡∏Ç‡πâ‡∏≠‡∏Ñ‡∏ß‡∏≤‡∏°‡πÑ‡∏õ‡∏¢‡∏±‡∏á‡∏¢‡∏±‡∏á Discord 1 ‡∏Ñ‡∏£‡∏±‡πâ‡∏á
  if(discord.test && SENSOR_STAT){
    if(discord.maxRequest == 0) return;
    if(sendMessageToDiscord()){
      discord.test = false;
      Serial.println("TEST Discord");
    } 
    discord.maxRequest --;   
  }
//-----------------------------------------------------------------------
  //  ‡∏™‡πà‡∏á‡∏Ç‡πâ‡∏≠‡∏Ñ‡∏ß‡∏≤‡∏°‡πÑ‡∏õ‡∏¢‡∏±‡∏á‡∏¢‡∏±‡∏á Local API 1 ‡∏Ñ‡∏£‡∏±‡πâ‡∏á
  if(api.test && SENSOR_STAT){
    if(api.maxRequest == 0) return;
    String payload = "{\"id\":\""+cfg.id+"\",\"alram\":"+digitalRead(SENSOR_PIN)+"}";
    if(sendDataToAPI(payload)){
      api.test = false;
      Serial.println("TEST Local API");
    } 
    api.maxRequest --;   
  }
//-----------------------------------------------------------------------  
  // for Local API ‡∏ó‡∏∏‡∏Å‡πÜ 2 ‡∏ß‡∏¥‡∏ô‡∏≤‡∏ó‡∏µ
  if (now - lastApiSend > 2000) {
    if(api.trigger){
      String payload = "{\"id\":\""+cfg.id+"\",\"alram\":"+digitalRead(SENSOR_PIN)+"}";
      sendDataToAPI(payload);      
    }
    //‡∏™‡πà‡∏á 1 ‡∏Ñ‡∏£‡∏±‡πâ‡∏á‡πÄ‡∏û‡∏∑‡πà‡∏≠‡∏õ‡∏¥‡∏î Siren
    if(!api.test && SENSOR_STAT){ 
      Serial.println(!api.test && SENSOR_STAT);
      api.test = true; 
      api.maxRequest = 5; 
    }
    lastApiSend = now;    
  }
//-----------------------------------------------------------------------
  // for Discord ‡∏ó‡∏∏‡∏Å 5 ‡∏ß‡∏¥‡∏ô‡∏≤‡∏ó‡∏µ  
  if (now - lastDiscordSend > 5000) {
    Serial.print("Trigger Discord : ");
    Serial.println(discord.trigger);
    if(discord.trigger){
      sendMessageToDiscord();      
    }
    //‡∏™‡πà‡∏á 1 ‡∏Ñ‡∏£‡∏±‡πâ‡∏á Discord
    if(!discord.test && SENSOR_STAT){ 
      //discord.test = true;
      discord.maxRequest = 5; 
    }
    lastDiscordSend = now;
  }
}
